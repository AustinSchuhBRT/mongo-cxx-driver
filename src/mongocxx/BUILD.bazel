load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@rules_cc//cc:defs.bzl", "cc_library")

package(default_visibility = ["//visibility:public"])

exports_files([
    "config/config.hpp.in",
    "config/private/config.hh.in",
    "config/version.hpp.in",
])

_defines = []

_ssl_defines = [
    "MONGOCXX_ENABLE_SSL=1",
]

write_file(
    name = "mongocxx_config",
    out = "config/config.hpp",
    content = [
        "#define MONGOCXX_INLINE_NAMESPACE_BEGIN",
        "#define MONGOCXX_INLINE_NAMESPACE_END",
    ],
)

write_file(
    name = "mongocxx_private_config",
    out = "config/private/config.hh",
    content = [
        "// Configuration done through bazel"
    ],
)

cc_library(
    name = "mongocxx_hdrs",
    strip_include_prefix = "/src",
    includes = ["."],
    hdrs = glob(["*.hpp"]) + [":mongocxx_config", ":mongocxx_private_config"],
    defines = _defines + select({
        "@bazelregistry_mongo_c_driver//:no_ssl": [],
        "@bazelregistry_mongo_c_driver//:openssl": _ssl_defines,
        "@bazelregistry_mongo_c_driver//:boringssl": _ssl_defines,
    }),
    deps = [
        "@mongocxx_config//:mongocxx_version",
    ],
)

cc_library(
    name = "mongocxx_private_hdrs",
    strip_include_prefix = "/src",
    includes = ["."],
    hdrs = ["test_util/mock.hh", "test_util/export_for_testing.hh", ":mongocxx_private_config"] + glob([
        "config/**/*.hpp",
        "config/**/*.hh",
        "events/**/*.hpp",
        "exception/**/*.hpp",
        "exception/**/*.hh",
        "gridfs/**/*.hpp",
        "gridfs/**/*.hh",
        "model/**/*.hpp",
        "options/**/*.hpp",
        "options/**/*.hh",
        "private/**/*.hpp",
        "private/**/*.hh",
        "result/**/*.hpp",
    ]),
    deps = [
        ":mongocxx_hdrs",
    ],
)

cc_library(
    name = "mongocxx",
    srcs = glob([
        "*.cpp",
        "config/**/*.cpp",
        "events/**/*.cpp",
        "exception/**/*.cpp",
        "gridfs/**/*.cpp",
        "model/**/*.cpp",
        "options/**/*.cpp",
        "private/**/*.cpp",
        "result/**/*.cpp",
    ]),
    deps = [
        ":mongocxx_hdrs",
        ":mongocxx_private_hdrs",
        "@bazelregistry_mongo_cxx_driver//src/bsoncxx",
        "@bazelregistry_mongo_c_driver//:libmongoc",
    ],
)
